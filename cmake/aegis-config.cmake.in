# ----------------------------------------------------------------------- #
# ------------------- Check that called with component ------------------ #
# ----------------------------------------------------------------------- #
if (NOT aegis_FIND_COMPONENTS)
    message(FATAL_ERROR "find_package(aegis) called with no component")
endif()
# ----------------------------------------------------------------------- #
# -------------- Set config_name dependent on libraries type ------------ #
# ----------------------------------------------------------------------- #
if(AEGIS_STATIC_LIBRARIES)
    set(config_name "static")
else()
    set(config_name "shared")
endif()
# ----------------------------------------------------------------------- #
# ---------------------- Update requested components -------------------- #
# ----------------------------------------------------------------------- #
set(FIND_aegis_ADDITIONAL_COMPONENTS "")

foreach(component ${aegis_FIND_COMPONENTS})
    list(APPEND FIND_aegis_ADDITIONAL_COMPONENTS ${FIND_aegis_${component}_DEPENDENCIES})
endforeach()

list(APPEND aegis_FIND_COMPONENTS ${FIND_aegis_ADDITIONAL_COMPONENTS})
list(REMOVE_DUPLICATES aegis_FIND_COMPONENTS)
# ----------------------------------------------------------------------- #
# -------------------- Load targets and dependencies -------------------- #
# ----------------------------------------------------------------------- #
set(targets_config_file ${CMAKE_CURRENT_LIST_DIR}/aegis-${config_name}-targets.cmake)
set(aegis_FOUND TRUE)

if(EXISTS ${targets_config_file})
    include(${targets_config_file})
    include("${CMAKE_CURRENT_LIST_DIR}/aegis-config-dependencies.cmake")

    if (aegis_FOUND)
        foreach (component ${aegis_FIND_COMPONENTS})
            if (TARGET aegis-${component})
                set(aegis_${component}_FOUND TRUE)
            else()
                set(FIND_aegis_ERROR "Found aegis but requested component '${component}' is missing in the config defined in ${AEGIS_DIR}.")
                set(aegis_${component}_FOUND FALSE)
                set(aegis_FOUND FALSE)
            endif()
        endforeach()
    endif()
else()
    set(FIND_aegis_ERROR "Requested aegis configuration (${config_name}) was not found")
    set(aegis_FOUND FALSE)
endif()
# ----------------------------------------------------------------------- #
# ------------=------------- Display all errors ------------------------- #
# ----------------------------------------------------------------------- #
if (NOT aegis_FOUND)
    if(aegis_FIND_REQUIRED)
        message(FATAL_ERROR "${FIND_aegis_ERROR}")
    elseif(NOT aegis_FIND_QUIETLY)
        message(STATUS "${FIND_aegis_ERROR}")
    endif()
endif()
# ----------------------------------------------------------------------- #
